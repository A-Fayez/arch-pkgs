# Maintainer:  Michael Daffin <michael@daffin.io>

_pkgname=vim-vue
pkgname=neovim-vue-git
pkgver=r86.a279172
pkgrel=1
pkgdesc="Syntax Highlight for Vue.js components"
arch=('any')
url="https://github.com/posva/vim-vue"
license=('MIT')
groups=('neovim-plugins')
depends=('neovim')
makedepends=('git')
source=('git+https://github.com/posva/vim-vue.git'
        'neovim-vue-shim.vim')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  local installpath="$pkgdir/usr/share/nvim/runtime.d"
  rm -rf "${_pkgname}"/{.git,.gitignore}
  mkdir -p "$installpath"
  cp -r "${_pkgname}" "$installpath/${_pkgname}"
  install -Dm644 neovim-vue-shim.vim "${pkgdir}/usr/share/nvim/runtime/plugin/neovim-vue-shim.vim"
}

# vim: ts=2 sw=2 et:
