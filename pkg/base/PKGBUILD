# Maintainer: Michael Daffin <michael@daffin.io>
pkgname=mdaffin-base
pkgver=1
pkgrel=1
pkgdesc="Base system configuration for mdaffin systems"
arch=('any')
url="https://github.com/mdaffin/mdaffin-arch-repo"
license=('MIT')
groups=('mdaffin')
install=mdaffin-base.install
provides=(
    'vim'
    'vi'
)
conflicts=(
    'vim'
    'vi'
)
depends=(
    'sudo'
    'neovim'
    'ripgrep'
    'exa'
    'fd-rs'
    'exfat-utils'
    'avahi'
    'nss-mdns'
    'python-neovim'
    'python2-neovim'
    'zsh'
    'grml-zsh-config'
    'tmux'
    'htop'
    'iotop'
    'openssh'
    'lsof'
    'wget'
    'f2fs-tools'
    'bash'
    'bzip2'
    'coreutils'
    'cryptsetup'
    'device-mapper'
    'dhcpcd'
    'diffutils'
    'file'
    'filesystem'
    'findutils'
    'gawk'
    'gcc-libs'
    'gettext'
    'glibc'
    'grep'
    'gzip'
    'inetutils'
    'iproute2'
    'iputils'
    'less'
    'licenses'
    'linux'
    'logrotate'
    'man-db'
    'man-pages'
    'mdadm'
    'pacman'
    'pciutils'
    'pcmciautils'
    'perl'
    'procps-ng'
    'psmisc'
    's-nail'
    'sed'
    'shadow'
    'sysfsutils'
    'systemd-sysvcompat'
    'tar'
    'texinfo'
    'usbutils'
    'util-linux'
    'which'
)
source=('vconsole.conf'
        'sudoers.wheel'
        'mdaffin-base.sh'
        'mdaffin-base.vim')

sha256sums=('c49f96d252b261c0d4f92f7f4bece4ec66a3dfdad0e9f91443069e09a1d26482'
            '9e2049e7013b49de7f5dd1d219b843f05ab889d89092f688acfb1cef7aef5940'
            '181ef0e34d4e897cb0ae17c16328114ec08719da42a46f05ff4b0c3a027b027a'
            '97df7abe0ac5b8047066394e3843f2227b2b7d5e5476f14f870a533b4f23d4b4')

package() {
    # TODO 'mdns_minimal [NOTFOUND=return] resolve' in /etc/nsswitch.conf
    install -dm 0755 "$pkgdir/usr/bin"
    ln -s /usr/bin/nvim "$pkgdir/usr/bin/vim"
    ln -s /usr/bin/nvim "$pkgdir/usr/bin/vi"
    install -Dm 0644 vconsole.conf "$pkgdir/etc/vconsole.conf"
    install -Dm 0640 sudoers.wheel "$pkgdir/etc/sudoers.d/wheel"
    install -Dm 0755 mdaffin-base.sh "$pkgdir/etc/profile.d/mdaffin-base.sh"
    install -Dm644 mdaffin-base.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-base.vim"
}
