# Maintainer: Michael Daffin <michael@daffin.io>
pkgbase='mdaffin'
pkgname=(
    mdaffin-base mdaffin-desktop mdaffin-router mdaffin-laptop mdaffin-dell-xps-13 mdaffin-devel
    mdaffin-devel-virtualisation mdaffin-devel-languages mdaffin-devel-embedded
    mdaffin-arch-repo mdaffin-devel-devops mdaffin-gaming
)
pkgver=30
pkgrel=3
pkgdesc="System config for mdaffin systems"
arch=(any)
url="https://github.com/mdaffin/arch-repo"
license=(MIT)
groups=(mdaffin)

rootdir=$PWD

package_mdaffin-base() {
    provides=(vim vi)
    conflicts=(vim vi)
    install=mdaffin-base.install

    # Base group
    depends=(bash bzip2 coreutils cryptsetup device-mapper dhcpcd diffutils
    e2fsprogs file filesystem findutils gawk gcc-libs gettext glibc grep gzip
    inetutils iproute2 iputils less licenses linux logrotate man-db man-pages
    mdadm pacman pciutils perl procps-ng psmisc s-nail sed shadow
    sysfsutils systemd-sysvcompat tar texinfo usbutils util-linux which)
    # Explicitly removed from the base group: jfsutils lvm2 nano netctl reiserfsprogs vi xfsprogs

    # Extra general packages
    depends+=(sudo ripgrep exa fd tmux wget fzf unzip dialog pacman-contrib bat ncdu keybase)

    # Debugging tools
    depends+=(lsof bind-tools mtr socat htop iotop openbsd-netcat strace)

    # Networking
    depends+=(wpa_supplicant iwd avahi nss-mdns openssh)

    # Shells
    depends+=(zsh grml-zsh-config)

    # Filesystems
    depends+=(exfat-utils dosfstools f2fs-tools)

    # Editor
    depends+=(oni neovim python-neovim neovim-ale neovim-fugitive)
    depends+=(gtk2) # Missing dependency for oni

    # Fonts
    depends+=(noto-fonts noto-fonts-cjk noto-fonts-emoji noto-fonts-extra
    ttf-monofur ttf-dejavu xorg-fonts-misc)

    cp -a "$rootdir/base/"* "$pkgdir"
}

package_mdaffin-desktop() {
    install=mdaffin-desktop.install
    depends=(mdaffin-base)

    # I3 Desktop
    depends+=(rofi i3-wm i3status i3blocks i3lock dmenu compton)

    # Login manager
    depends+=(sddm rng-tools)

    # Applications
    depends+=(alacritty firefox)

    # Utility
    depends+=(redshift python-gobject bluez bluez-utils pulseaudio pulseaudio-alsa pulseaudio-bluetooth pavucontrol paprefs scrot)

    # Drivers and utilities
    depends+=(libinput-gestures xf86-input-libinput xf86-input-synaptics
    xf86-video-intel xf86-video-vesa nvidia nvidia-utils)

    # Xorg packages and utilities
    depends+=(xorg-xsetroot xorg-xkill xorg-xbacklight xorg-bdftopcf
    xorg-iceauth xorg-luit xorg-mkfontdir xorg-mkfontscale xorg-sessreg
    xorg-setxkbmap xorg-smproxy xorg-x11perf xorg-xauth xorg-xbacklight
    xorg-xcmsdb xorg-xcursorgen xorg-xdpyinfo xorg-xdriinfo xorg-xev
    xorg-xgamma xorg-xhost xorg-xinput xorg-xkbcomp xorg-xkbevd xorg-xkbutils
    xorg-xkill xorg-xlsatoms xorg-xlsclients xorg-xmodmap xorg-xpr xorg-xprop
    xorg-xrandr xorg-xrdb xorg-xrefresh xorg-xset xorg-xsetroot xorg-xvinfo
    xorg-xwd xorg-xwininfo xorg-xwud)

    cp -a "$rootdir/desktop/"* "$pkgdir"
}

package_mdaffin-router() {
    depends=(rp-pppoe dnsmasq shorewall)

    cp -a "$rootdir/router/"* "$pkgdir"
}

package_mdaffin-gaming() {
    depends=(mdaffin-desktop steam nvidia nvidia-utils lib32-nvidia-utils
    lib32-libpulse ttf-liberation wqy-zenhei)
}

package_mdaffin-laptop() {
    depends=('mdaffin-desktop' 'powertop')
}

package_mdaffin-dell-xps-13() {
    depends=('mdaffin-laptop')

    cp -a "$rootdir/desktop/"* "$pkgdir"
}

package_mdaffin-devel() {
    depends=(mdaffin-base)

    # Base Group
    depends+=(autoconf automake binutils bison fakeroot file findutils flex
    gawk gcc gettext grep groff gzip libtool m4 make pacman patch pkg-config
    sed sudo systemd texinfo util-linux which)

    # General tools
    depends+=(git cmake jq)
}

package_mdaffin-devel-virtualisation() {
    depends=(mdaffin-devel)

    # Virtualisation
    depends+=(virtualbox virtualbox-host-modules-arch vagrant)

    # Docker
    depends+=(docker docker-compose dnsmasq)
}

package_mdaffin-devel-languages() {
    depends=(mdaffin-devel)

    # Rust
    depends+=(neovim-rust-git rustup)

    # Go
    depends+=(go neovim-go)

    # Node
    depends+=(yarn npm nodejs prettier vale)

    cp -a "$rootdir/devel-languages/"* "$pkgdir"
}

package_mdaffin-devel-embedded() {
    depends=(mdaffin-devel)

    depends+=(mdaffin-devel qemu qemu-arch-extra picocom arm-linux-gnueabihf-gcc ddrescue)
}

package_mdaffin-arch-repo() {
    depends=(mdaffin-devel)
    conflicts=(repose)

    depends+=(mdaffin-devel devtools aurutils s3cmd)
}

package_mdaffin-devel-devops() {
    depends=(mdaffin-devel-virtualisation mdaffin-devel-languages terraform minikube kubernetes-helm kubectl-bin)
}
