# Maintainer: Michael Daffin <michael@daffin.io>
pkgbase='mdaffin'
pkgname=('mdaffin-base' 'mdaffin-desktop' 'mdaffin-laptop'
'mdaffin-dell-xps-13' 'mdaffin-devel' 'mdaffin-devel-vagrant'
'mdaffin-devel-docker' 'mdaffin-devel-rust' 'mdaffin-devel-go'
'mdaffin-devel-web' 'mdaffin-devel-embedded' 'mdaffin-devel-arch-repo'
'mdaffin-devel-work')
pkgver=6
pkgrel=2
pkgdesc="System config for mdaffin systems"
arch=('any')
url="https://github.com/mdaffin/arch-repo"
license=('MIT')
groups=('mdaffin')

source=(
    'mdaffin-base.sh'
    'mdaffin-base.vim'
    'mdaffin-base.sudoers'
    'mdaffin-base-vconsole.conf'
    'mdaffin-base-enp.network'
    'mdaffin-base-wlp.network'
    'mdaffin-desktop-sddm.conf'
    'mdaffin-desktop-00-keyboard.conf'
    'mdaffin-desktop-i3-config'
    'mdaffin-desktop.termite'
    'mdaffin-desktop.sh'
    'mdaffin-dell-xps-13-hidpi.sh'
    'mdaffin-dell-xps-13-hidpi-monitor.conf'
    'mdaffin-devel-rust.vim'
    'mdaffin-devel-go.vim'
)

sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

package_mdaffin-base() {
    provides=('vim' 'vi')
    conflicts=('vim' 'vi')
    install=mdaffin-base.install

    depends=('sudo' 'neovim' 'ripgrep' 'exa' 'fd' 'exfat-utils' 'avahi'
    'nss-mdns' 'python-neovim' 'python2-neovim' 'zsh' 'grml-zsh-config' 'tmux'
    'htop' 'iotop' 'openssh' 'lsof' 'wget' 'f2fs-tools' 'bash' 'bzip2'
    'coreutils' 'cryptsetup' 'device-mapper' 'dhcpcd' 'diffutils' 'file'
    'filesystem' 'findutils' 'gawk' 'gcc-libs' 'gettext' 'glibc' 'grep' 'gzip'
    'inetutils' 'iproute2' 'iputils' 'less' 'licenses' 'linux' 'logrotate'
    'man-db' 'man-pages' 'mdadm' 'pacman' 'pciutils' 'pcmciautils' 'perl'
    'procps-ng' 'psmisc' 's-nail' 'sed' 'shadow' 'sysfsutils' 'wpa_supplicant'
    'systemd-sysvcompat' 'tar' 'texinfo' 'usbutils' 'util-linux' 'which'
    'neovim-languageclient' 'nvim-completion-manager-git' 'neovim-fugitive')

    install -dm 0755 "$pkgdir/usr/bin"
    ln -s /usr/bin/nvim "$pkgdir/usr/bin/vim"
    ln -s /usr/bin/nvim "$pkgdir/usr/bin/vi"

    install -Dm 0644 mdaffin-base-enp.network "$pkgdir/usr/lib/systemd/network/90-mdaffin-base-enp.network"
    install -Dm 0644 mdaffin-base-wlp.network "$pkgdir/usr/lib/systemd/network/90-mdaffin-base-wlp.network"
    install -Dm 0644 mdaffin-base-vconsole.conf "$pkgdir/etc/vconsole.conf"
    install -Dm 0640 mdaffin-base.sudoers "$pkgdir/etc/sudoers.d/mdaffin-base"
    install -Dm 0755 mdaffin-base.sh "$pkgdir/etc/profile.d/mdaffin-base.sh"
    install -Dm644 mdaffin-base.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-base.vim"
}

package_mdaffin-desktop() {
    install=mdaffin-desktop.install

    depends=('mdaffin-base' 'rofi' 'libinput-gestures' 'ttf-monofur'
    'xf86-input-libinput' 'xf86-input-synaptics' 'xf86-video-intel'
    'xf86-video-nouveau' 'nvidia' 'xf86-video-vesa' 'xorg-xsetroot'
    'xorg-xkill' 'xorg-xbacklight' 'redshift' 'python-xdg' 'sddm' 'i3-wm'
    'i3status' 'i3blocks' 'i3lock' 'dmenu' 'termite' 'firefox' 'ttf-dejavu'
    'bluez' 'openvpn' 'pulseaudio' 'pulseaudio-alsa' 'pavucontrol'
    'xorg-bdftopcf' 'xorg-iceauth' 'xorg-luit' 'xorg-mkfontdir'
    'xorg-mkfontscale' 'xorg-sessreg' 'xorg-setxkbmap' 'xorg-smproxy'
    'xorg-x11perf' 'xorg-xauth' 'xorg-xbacklight' 'xorg-xcmsdb'
    'xorg-xcursorgen' 'xorg-xdpyinfo' 'xorg-xdriinfo' 'xorg-xev' 'xorg-xgamma'
    'xorg-xhost' 'xorg-xinput' 'xorg-xkbcomp' 'xorg-xkbevd' 'xorg-xkbutils'
    'xorg-xkill' 'xorg-xlsatoms' 'xorg-xlsclients' 'xorg-xmodmap' 'xorg-xpr'
    'xorg-xprop' 'xorg-xrandr' 'xorg-xrdb' 'xorg-xrefresh' 'xorg-xset'
    'xorg-xsetroot' 'xorg-xvinfo' 'xorg-xwd' 'xorg-xwininfo' 'xorg-xwud')

    install -Dm 0644 mdaffin-desktop-sddm.conf "$pkgdir/etc/sddm.conf"
    install -Dm 0644 mdaffin-desktop-00-keyboard.conf "$pkgdir/etc/X11/xorg.conf.d/00-keyboard.conf"
    install -Dm 0644 mdaffin-desktop-i3-config "$pkgdir/etc/i3/mdaffin-desktop-config"
    install -Dm 0644 mdaffin-desktop.termite "$pkgdir/etc/xdg/termite/mdaffin-desktop.config"
    install -Dm 0755 mdaffin-desktop.sh "$pkgdir/etc/X11/xinit/xinitrc.d/30-mdaffin-desktop.sh"
}

package_mdaffin-laptop() {
    depends=('mdaffin-desktop' 'powertop')
}

package_mdaffin-dell-xps-13() {
    depends=('mdaffin-laptop')

    install -Dm 0755 mdaffin-dell-xps-13-hidpi.sh "$pkgdir/etc/X11/xinit/xinitrc.d/20-hidpi.sh"
    install -Dm 0644 mdaffin-dell-xps-13-hidpi-monitor.conf "$pkgdir/etc/X11/xorg.conf.d/90-hidpi-monitor.conf"
}

package_mdaffin-devel() {
    depends=('mdaffin-base' 'git' 'cmake'
    'jq' 'autoconf' 'automake' 'binutils' 'bison' 'fakeroot' 'file'
    'findutils' 'flex' 'gawk' 'gcc' 'gettext' 'grep' 'groff' 'gzip' 'libtool'
    'm4' 'make' 'pacman' 'patch' 'pkg-config' 'sed' 'texinfo' 'util-linux'
    'which')
}

package_mdaffin-devel-vagrant() {
    depends=('mdaffin-base' 'virtualbox' 'virtualbox-host-modules-arch'
    'vagrant')
}
package_mdaffin-devel-docker() {
    depends=('mdaffin-base' 'docker' 'docker-compose' 'dnsmasq')
}

package_mdaffin-devel-rust() {
    depends=('mdaffin-devel' 'neovim-rust-git' 'rustup')

    install -Dm644 mdaffin-devel-rust.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-rust.vim"
}

package_mdaffin-devel-go() {
    depends=('mdaffin-devel' 'go' 'neovim-go')

    install -Dm644 mdaffin-devel-go.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-go.vim"
}

package_mdaffin-devel-web() {
    depends=('mdaffin-devel' 'yarn' 'nodejs')
}

package_mdaffin-devel-embedded() {
    depends=('mdaffin-devel' 'qemu' 'qemu-arch-extra' 'picocom' )
}

package_mdaffin-devel-arch-repo() {
    depends=('mdaffin-devel' 'devtools' 'repose' 'aurutils')
}

package_mdaffin-devel-work() {
    depends=('mdaffin-devel-docker' 'mdaffin-devel-vagrant' 'mdaffin-devel-go' 'mdaffin-devel-rust' 'mdaffin-devel-web')
}
